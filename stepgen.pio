;
; Copyright (c) 2020 Raspberry Pi (Trading) Ltd.
;
; SPDX-License-Identifier: BSD-3-Clause
;
.pio_version 0 // only requires PIO version 0

.program stepgen

; Genera una onda cuadrada leyendo tiempos en alto/bajo por FIFO en cada ciclo (pensado para DMA)
public dma_stream:
    pull block      ; Recibir tiempo en alto
    mov y, osr
    set pins, 1
dma_high:
    jmp y-- dma_high

    pull block      ; Recibir tiempo en bajo
    mov y, osr
    set pins, 0
dma_low:
    jmp y-- dma_low
    jmp dma_stream

; Genera una onda cuadrada continua
public infinite:
    pull block      ; Recibir tiempo en alto
    mov isr, osr    ; Guardar en ISR
    pull block      ; Recibir tiempo en bajo
inf_loop:
    mov y, isr      ; Cargar tiempo alto
    set pins, 1     ; Pin en Alto
inf_high:
    jmp y-- inf_high

    mov y, osr      ; Cargar tiempo bajo
    set pins, 0     ; Pin en Bajo
inf_low:
    jmp y-- inf_low
    jmp inf_loop

; Genera una ráfaga (burst) de N ciclos
public burst:
    pull block      ; Recibir cantidad de ciclos (N-1)
    mov x, osr      ; Guardar en X
    pull block      ; Recibir tiempo en alto
    mov isr, osr    ; Guardar en ISR
    pull block      ; Recibir tiempo en bajo
burst_loop:
    mov y, isr      ; Cargar tiempo alto
    set pins, 1     ; Pin en Alto
bur_high:
    jmp y-- bur_high

    mov y, osr      ; Cargar tiempo bajo
    set pins, 0     ; Pin en Bajo
bur_low:
    jmp y-- bur_low
    jmp x-- burst_loop

% c-sdk {
static inline void stepgen_program_init(PIO pio, uint sm, uint offset, uint pin) {
    pio_sm_config c = stepgen_program_get_default_config(offset);

    // Asigna el grupo de pines SET a 1 pin (salida de pulsos).
    sm_config_set_set_pins(&c, pin, 1);
    // Conecta el PIO al pad GPIO
    pio_gpio_init(pio, pin);
    // Dirección de salida
    pio_sm_set_consecutive_pindirs(pio, sm, pin, 1, true);

    // Carga la configuración y arranca la SM
    pio_sm_init(pio, sm, offset, &c);
    pio_sm_set_enabled(pio, sm, true);
}
%}
